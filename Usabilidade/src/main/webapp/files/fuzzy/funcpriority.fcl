

FUNCTION_BLOCK fuzzy		// Block definition (there may be more than one block per file)

VAR_INPUT					// Define input variables
	effectiveness : REAL;
	efficiency : REAL;
END_VAR

VAR_OUTPUT					// Define output variable
	priority : REAL;
END_VAR

FUZZIFY effectiveness		// Fuzzify input variable 'effectiveness': {'poor', 'moderate' , 'good'}
	TERM poor := 			(0, 1) (0.4, 0) ; 
	TERM moderate := 		(0.2, 0) (0.5,1) (0.8,0);
	TERM good := 			(0.6, 0) (1, 1);
END_FUZZIFY

FUZZIFY efficiency			// Fuzzify input variable 'efficiency': {'poor', 'moderate' , 'good'}
	TERM poor := 			(0, 1) (0.4, 0) ; 
	TERM moderate := 		(0.2, 0) (0.5,1) (0.8,0);
	TERM good := 			(0.6, 0) (1, 1);
END_FUZZIFY

DEFUZZIFY priority			// Defuzzify output variable 'priority' : {'low', 'average' , 'high'}
	TERM low := 			(0, 1) (0.25, 0) ; 
	TERM average := 		(0.25, 0) (0.5,1) (0.75,0);
	TERM high := 			(0.75, 0) (1, 1);
	METHOD : COG;			// Use 'Center Of Gravity' defuzzification method
	DEFAULT := 0;			// Default value is 0 (if no rule activates defuzzifier)
END_DEFUZZIFY

RULEBLOCK No1
	AND : MIN;				// Use 'min' for 'and' (also implicit use 'max' for 'or' to fulfill DeMorgan's Law)
	ACT : MIN;				// Use 'min' activation method
	ACCU : MAX;				// Use 'max' accumulation method

	RULE 1 : 	IF effectiveness IS poor 		AND efficiency IS poor 		THEN priority IS high;
	RULE 2 : 	IF effectiveness IS poor 		AND efficiency IS moderate 	THEN priority IS high;
	RULE 3 : 	IF effectiveness IS poor 		AND efficiency IS good	 	THEN priority IS high;
	RULE 4 : 	IF effectiveness IS moderate 	AND efficiency IS poor 		THEN priority IS high;
	RULE 5 : 	IF effectiveness IS moderate 	AND efficiency IS moderate 	THEN priority IS average;
	RULE 6 : 	IF effectiveness IS moderate 	AND efficiency IS good 		THEN priority IS average;
	RULE 7 : 	IF effectiveness IS good 		AND efficiency IS poor 		THEN priority IS average;
	RULE 8 : 	IF effectiveness IS good 		AND efficiency IS moderate 	THEN priority IS low;
	RULE 9 : 	IF effectiveness IS good 		AND efficiency IS good 		THEN priority IS low;
	
END_RULEBLOCK

END_FUNCTION_BLOCK